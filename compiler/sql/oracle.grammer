:: stms

stms = (@+ stm ';').

stm = (@or create_table:create-table-stm).

identifier = (@! (@or (@! /^[[:alnum:]_]+$/)
                      'CONSTRAINT')). ; TODO

create-table-stm =
  'CREATE' 'TABLE' table '('
    create-table-item (@* ',' create-table-item)
  ')'.

table = table:((@? schema-name '.') table-name).
schema-name = identifier.
table-name = identifier.

create-table-item = (@or column_definition:column-definition
                         constraint:table-level-constraint).

column-definition =
  simple-column-name DataType
  (@* column-level-constraint)
  (@? (@? 'WITH') 'DEFAULT' DefaultConstantExpression)
  (@* column-level-constraint).
DataType =
  (@or character-datatype
       number-datatype
       datetime-datatype
       large-object-datatype).
character-datatype =
  varchar2:('VARCHAR2' '(' size ')').
number-datatype =
  (@or number:('NUMBER' (@? '(' size ')'))  ; TODO precision
       float:('FLOAT' (@? '(' size ')'))).
datetime-datatype =
  (@or date:'DATE' timestamp:'TIMESTAMP').  ; TODO
large-object-datatype =
  (@or blob:'BLOB' clob:'CLOB' nclob:'NCLOB' bfile:'BFILE').
size = /\d+/.
;DataType = identifier (@? '(' identifier ')'). ; TODO
DefaultConstantExpression =
  (@or 'NULL'
       ('CURRENT' (@or 'SCHEMA' 'SQLID'))
       'USER' 'CURRENT_USER' 'SESSION_USER' 'CURRENT_ROLE'
       'DATE'
       'TIME'
       'TIMESTAMP'
       ('CURRENT' 'DATE') 'CURRENT_DATE'
       ('CURRENT' 'TIME') 'CURRENT_TIME'
       ('CURRENT' 'TIMESTAMP') 'CURRENT_TIMESTAMP'
       ).
       ;literal).

;literal = ??TODO

simple-column-name = identifier.
simple-column-names = simple-column-name (@* ',' simple-column-name).

column-level-constraint =
  (@? 'CONSTRAINT' constraint-name)
  (@or not_null:('NOT' 'NULL')
       ; check:('CHECK' search-condition)  ; TODO
       primary_key:('PRIMARY' 'KEY')
       unique:('UNIQUE')
       references_clause:references-clause).

constraint-name = identifier.

references-clause =
  'REFERENCES' table
  (@? '(' simple-column-names ')')
  (@or ((@? on-delete) (@? on-update))
       ((@? on-update) (@? on-delete))).
on-delete =
  'ON' 'DELETE' (@or ('NO' 'ACTION') 'RESTRICT' 'CASCADE' 'SET NULL').
on-update =
  'ON' 'UPDATE' (@or ('NO' 'ACTION') 'RESTRICT').

table-level-constraint =
  (@? 'CONSTRAINT' constraint-name)
  (@or ; check:('CHECK' search-condition)  ; TODO
       primary_key:('PRIMARY' 'KEY' '(' simple-column-names ')')
       unique:('UNIQUE' '(' simple-column-names ')')
       foreign_key:('FOREIGN' 'KEY' '(' simple-column-names ')'
                              references-clause)).
